/*
 * Copyright (C) 2017 TypeFox and others.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
 */
module io.typefox.sprotty.example.multicore.GenerateMulticoreAllocation

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."
var fileHeader = "/*
 * Copyright (C) 2017 TypeFox and others.
 *
 * Licensed under the Apache License, Version 2.0 (the \"License\"); you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
 */"

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "io.typefox.sprotty.example.multicore"
				rootPath = rootPath
				web = {
					enabled = true
				}
				mavenLayout = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = fileHeader
			}
		}
		language = StandardLanguage {
			name = "io.typefox.sprotty.example.multicore.MulticoreAllocation"
			fileExtensions = "multicore"

			scopeProvider = {
				generateStub = false
			}
			generator = {
				generateStub = false
			}
			serializer = {
				generateStub = false
			}
			webSupport = {
				framework = "Ace"
				generateJettyLauncher = false
				generateHtmlExample = false
			}
		}
	}
}
